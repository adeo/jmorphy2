subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    group = 'company.evo.jmorphy2'
    def libVersion = rootProject.file('project.version')
            .readLines()
            .first()
            .toUpperCase()
    if (project.hasProperty("release")) {
        libVersion = libVersion - "-SNAPSHOT"
    }
    version = libVersion

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked"

    javadoc.options.charSet = 'UTF-8'

    ext {
        defaultEsVersion = rootProject.file('es.version')
                .readLines()
                .first()
        defaultLuceneVersion = '8.7.0'

        esLuceneVersions = [
                '6.6': '7.6.0',
                '6.7': '7.7.0',
                '6.8': '7.7.3',
                '7.0': '8.0.0',
                '7.1': '8.0.0',
                '7.2': '8.0.0',
                '7.3': '8.1.0',
                '7.4': '8.2.0',
                '7.5': '8.3.0',
                '7.6': '8.4.0',
                '7.7': '8.5.1',
                '7.8': '8.5.1',
                '7.9': '8.6.2',
                '7.10': '8.7.0',
                '7.11': defaultLuceneVersion
        ]
        elasticsearchVersion = project.hasProperty('esVersion') ? project.getProperty('esVersion') : defaultEsVersion
        luceneVersion = esLuceneVersions.get(
                elasticsearchVersion,
                esLuceneVersions.get((elasticsearchVersion =~ /^(\d+\.\d+)/)[0][0], defaultLuceneVersion)
        )
    }

    repositories {
        mavenCentral()
    }

    test {
        exclude '**/*Benchmark*'
    }
}
